/*
 * Copyright (c) 2007-2013 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
        maven { url 'http://repo.springsource.org/plugins-release' }
  }
  dependencies {
    classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.4'
    classpath 'eu.appsatori:gradle-fatjar-plugin:0.2-rc1'

  }
}

// gives 'provided' scope for hadoop
configure(allprojects) {
  apply plugin: 'propdeps'
  apply plugin: 'propdeps-maven'
  apply plugin: 'propdeps-idea'
  apply plugin: 'propdeps-eclipse'
}

apply from: 'etc/version.gradle'

group = 'cascading'
version = releaseVersion

// the version of hadoop used for compiling and testing
ext.hadoopVersion = "1.1.2"

ext.cascadingVersion = '2.2.0-+'

// set property to use locally installed cascading builds
// otherwise wip releases will be pulled from conjars
if( System.properties[ 'cascading-jdbc.release.local' ] ) 
  cascadingVersion = '2.2.0-wip-dev'

subprojects{

  repositories{
    mavenLocal()
    mavenCentral()
    mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
  }

  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'maven'
  apply plugin: 'eclipse'
  apply plugin: 'fatjar'

  dependencies{
    compile group: 'cascading', name: 'cascading-local', version: cascadingVersion
    compile group: 'cascading', name: 'cascading-hadoop', version: cascadingVersion


    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'
    testCompile group: 'cascading', name:'cascading-core', version: cascadingVersion, classifier: 'tests'
    testCompile( group: 'org.apache.hadoop', name: 'hadoop-test', version: hadoopVersion ){
      exclude group: 'oro'
      exclude group: 'hsqldb'
    }

    provided( group: 'org.apache.hadoop', name: 'hadoop-core', version: hadoopVersion) {
      exclude group: 'ant'
      exclude group: 'junit'
      exclude group: 'hsqldb'
      exclude group: 'oro' // causes transient build maven failures, ftw
    }

    provided( group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1')
  }
}


artifacts{
  subprojects{
    archives fatJar
  }
}


idea {
  module {
      downloadJavadoc = true
      downloadSources = true
  }
}

eclipse{
  classpath{
    defaultOutputDir = file('build')
    downloadSources = true
    downloadJavadoc = true
  }
}

uploadArchives {

  repositories.mavenDeployer {
    configuration = configurations.archives

    repository( url: repoUrl ) { 
      authentication( userName: repoUserName, password: repoPassword )
    }   

    pom.project {
      description 'Cascading JDBC is a collection of adapters for JDBC.'
      inceptionYear '2013'
      url 'http://cascading.org/'
      scm {
        url 'https://github.com/Cascading/cascading-jdbc.git'
      }   
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }   
      }   
    }   
  }
}
